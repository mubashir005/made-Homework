// SPDX-License-Identifier: AGPL-3.0-only

pipeline SheetPipeline {

    SheetExtractor
        -> SheetExcelInterpreter
        -> SheetSheetPicker
        -> SheetHeaderWriter
        -> SheetRowDeleter
        -> SheetTableInterpreter1
        -> SaveGDPTableLoader;

    SheetRowDeleter
        -> SheetTableInterpreter2
        -> SaveBondIssuanceTableLoader;

    block SheetExtractor oftype HttpExtractor {
        url: "https://thedocs.worldbank.org/en/doc/7d852628d96b9411d43e5d36d5dff941-0050062022/original/Graphs-Chapter-5-02082022.xlsx";
    }

    block SheetExcelInterpreter oftype XLSXInterpreter { }

    block SheetSheetPicker oftype SheetPicker {
        sheetName: "Figure S5.1.2";
    }

    block SheetHeaderWriter oftype CellWriter {
        at: range P2:S2;
        write: [
            "Country Code",
            "Economy",
            "GDP per Capita",
            "Bond Issuance Share"
        ];
    }

    block SheetRowDeleter oftype RowDeleter {
        delete: [
            row 1,
            row 17,
            row 21,
            row 28,
            row 42,
            row 44,
            row 45
        ];
    }

    block SheetTableInterpreter1 oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "Bond Issuance Share" oftype decimal
        ];
    }

    block SheetTableInterpreter2 oftype TableInterpreter {
        header: true;
        columns: [
            "Country Code" oftype CountryCodeAlpha3,
            "GDP per Capita" oftype decimal
        ];
    }

    block SaveGDPTableLoader oftype SQLiteLoader {
        table: "gdpPerCapita";
        file: "./country-stats.sqlite";
        dropTable: true;
        
    }

    block SaveBondIssuanceTableLoader oftype SQLiteLoader {
        table: "bondIssuance";
        file: "./country-stats.sqlite";
        dropTable: false;
    }
}


